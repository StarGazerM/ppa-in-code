// flow & reverse flow graph

// Yihao Sun
// 2020 Syracuse

#include "lang.dl"

// the inital flow id of statement `sid` is `l`
.decl Init(sid: number, l: number)
Init(id, id) :- Assign(id, _, _).
Init(id, id) :- Skip(id).
Init(sid, l) :- Seq(sid, s1, _), Init(s1, l).
Init(ifid, guard) :- If(ifid, guard, _, _).
Init(whileid, guard) :- While(whileid, guard, _).
Init(id, lc) :- Call(id, lc, _, _, _, _).
Init(ln, ln) :- ProcDecl(ln, _, _, _, _).

// the final flow id of statement `sid` is `l
.decl Final(sid: number, l: number)
Final(id, id) :- Assign(id, _, _).
Final(id, id) :- Skip(id).
Final(sid, l) :- Seq(sid, _, s2), s2!=-1, Final(s2, l).
// end of seq
Final(sid, l) :- Seq(sid, s1, s2), s2=-1, Final(s1, l).
Final(ifId, l) :- If(ifId, _, trueId, _), Final(trueId, l).
Final(ifId, l) :- If(ifId, _, _, falseId), Final(falseId, l).
Final(whileId, guardId) :- While(whileId, guardId, _).
Final(callId, lr) :- Call(callId, _, lr, _, _, _).
Final(lx, lx) :- ProcBody(lx, _).

// statement a flows to statement 2        
.decl Flow(from: number, to: number)
.output Flow
Flow(from, to) :-
   Seq(_, s1, nextId),
   nextId!=-1,
   Seq(nextId, s2, _),
   Final(s1, from),
   Init(s2, to).
Flow(from, to) :-
   If(_, from, trueId, _), Init(trueId, to).
Flow(from, to) :-
   If(_, from, _, falseId), Init(falseId, to).
Flow(from, to) :-
   While(_, from, bodyId),
   Init(bodyId, to).
Flow(from, to) :-
   While(_, to, bodyId),
   Final(bodyId, from).
Flow(ln, to) :-
   Init(stmt, to),
   ProcBody(ln, stmt).
Flow(from, lx) :-
   ProcDecl(lx, _, _, _, ln),
   ProcBody(ln, stmt),
   Final(stmt, from).

.decl FlowReturn(from: number, to: number)
.output FlowReturn
FlowReturn(lx, lr) :-
   Call(_, _, lr, pname, _, _),
   ProcDecl(lx, pname, _, _, _).

.decl FlowCall(from: number, to: number)
.output FlowCall
FlowCall(lc, ln) :-
   Call(_, lc, _, pname, _, _),
   ProcDecl(lx, pname, _, _, ln),
   ProcBody(ln, _).


.decl FlowReverse(from: number, to: number)
.output FlowReverse
FlowReverse(from, to) :- Flow(to, from).